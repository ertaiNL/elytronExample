echo ##### Custom role mapper
if (outcome != success) of /subsystem=elytron/custom-role-mapper=ExampleRoleMapper:read-resource
    /subsystem=elytron/custom-role-mapper=ExampleRoleMapper:add( \
        module=nl.ertai.elytron.example.security-module, \
        class-name=nl.ertai.elytron.example.ExampleRoleMapper, \
        configuration={ \
            "RoleToMap"="Unmapped", \
            "RoleToMapTo"="Mapped"} )
end-if

echo ##### Custom realm from the security-module
if (outcome != success) of /subsystem=elytron/custom-realm=DefaultRealm:read-resource
    /subsystem=elytron/custom-realm=DefaultRealm:add( \
        module=nl.ertai.elytron.example.security-module, \
        class-name=nl.ertai.elytron.example.ExampleRealm, \
        configuration={ \
            "User"="admin", \
            "Password"="password", \
            "Groups"="Admin,User,Unmapped" } )
end-if

echo ##### JNDI variabeles for JNDI Realm
if (outcome != success) of /subsystem=naming/binding=java\:global\/example\/user:read-resource
  /subsystem=naming/binding=java\:global\/example\/user:add(binding-type=simple, value="user")
end-if
if (outcome != success) of /subsystem=naming/binding=java\:global\/example\/password:read-resource
  /subsystem=naming/binding=java\:global\/example\/password:add(binding-type=simple, value="password")
end-if
if (outcome != success) of /subsystem=naming/binding=java\:global\/example\/groups:read-resource
  /subsystem=naming/binding=java\:global\/example\/groups:add(binding-type=simple, value="Other,User,Unmapped")
end-if


echo ##### Custom realm from the security-module
if (outcome != success) of /subsystem=elytron/custom-realm=JNDIRealm:read-resource
    /subsystem=elytron/custom-realm=JNDIRealm:add( \
        module=nl.ertai.elytron.example.security-module, \
        class-name=nl.ertai.elytron.example.ExampleJNDIRealm )
end-if

echo ##### Custom realm mapper
if (outcome != success) of /subsystem=elytron/custom-realm-mapper=ExampleRealmMapper:read-resource
    /subsystem=elytron/custom-realm-mapper=ExampleRealmMapper:add( \
        module=nl.ertai.elytron.example.security-module, \
        class-name=nl.ertai.elytron.example.ExampleRealmMapper, \
        configuration={ \
            "UserToMap"="user", \
            "RealmToMapTo"="JNDIRealm"} )
end-if

echo ##### Security Domain used in the application
if (outcome != success) of /subsystem=elytron/security-domain=ExampleDomain:read-resource
    /subsystem=elytron/security-domain=ExampleDomain:add( \
        permission-mapper="default-permission-mapper", \
        default-realm="DefaultRealm", \
        realm-mapper="ExampleRealmMapper", \
        realms=[ \
            {"realm"="DefaultRealm","role-decoder"="groups-to-roles"}, \
            {"realm"="JNDIRealm","role-decoder"="groups-to-roles","role-mapper"="ExampleRoleMapper"} ] )
end-if

echo ##### Set Security Domain for the webservlet and set it default
if (outcome != success) of /subsystem=elytron/http-authentication-factory=ExampleDomain-http-authenticator:read-resource
    /subsystem=elytron/http-authentication-factory=ExampleDomain-http-authenticator:add( \
        http-server-mechanism-factory=global, \
        security-domain=ExampleDomain, \
        mechanism-configurations=[ { mechanism-name=BASIC } ] )
end-if

if (outcome != success) of /subsystem=undertow/application-security-domain=ExampleDomain:read-resource
    /subsystem=undertow/application-security-domain=ExampleDomain:add( \
        http-authentication-factory=ExampleDomain-http-authenticator)
end-if

/subsystem=undertow:write-attribute( \
    name=default-security-domain, \
    value=ExampleDomain )

echo ##### Set Security Domain for EJB's and set it default
if (outcome != success) of /subsystem=elytron/sasl-authentication-factory=ExampleDomain-sasl-authenticator:read-resource
    /subsystem=elytron/sasl-authentication-factory=ExampleDomain-sasl-authenticator:add( \
        sasl-server-factory=configured, \
        security-domain=ExampleDomain, \
        mechanism-configurations=[ { mechanism-name=BASIC } ] )

end-if

if (outcome != success) of /subsystem=ejb3/application-security-domain=ExampleDomain:read-resource
    /subsystem=ejb3/application-security-domain=ExampleDomain:add( \
        security-domain=ExampleDomain)
end-if

/subsystem=ejb3:write-attribute( \
    name=default-security-domain, \
    value=ExampleDomain)

reload